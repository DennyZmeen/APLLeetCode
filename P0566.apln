:Namespace P0566
    ⍝ LeetCode problem #566                                     
    ⍝ Reshape the Matrix, https://leetcode.com/problems/reshape-the-matrix/
    (⎕IO ⎕ML) ← 0 1       

    ⍝ Check if the new shape holds the same number of elements,
    ⍝ and use the standard reshape (⍴) if so.
    Sol ← {
        (×/⍺)≠×/⍴⍵: ⍵
        ⍺⍴⍵
    }
    
    ⍝ == A couple of contributions by @abrudz ==
    ⍝ A branch-less solution
    Sol2 ← {⍺⍴⍣((×/⍺)=×/⍴⍵)⊢⍵}
    ⍝ Try the reshape, then see if the ravels match
    Sol3 ← {
        ⍵≢⍥,r←⍺⍴⍵:⍵
        r
    }
    ⍝ Based on if the ravels match, pick the result
    Sol4 ← ⍴(≢⍥,⊃,⍥⊂)⊢                                 
:EndNamespace

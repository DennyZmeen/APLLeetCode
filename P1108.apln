:Namespace P1108
    ⍝ LeetCode problem #1108
    ⍝ Defanging an IP Address, https://leetcode.com/problems/defanging-an-ip-address
    (⎕IO ⎕ML) ← 0 1
            
    ⍝ (Tacit) regex solution
    Sol ← '\.'⎕R'[.]'
    
    ⍝ (Tacit) split on '.', rejoin with '[.]'
    Sol2 ← ⊃((⊣,'[.]',⊢)/'.'∘≠⊆⊢)
    
    ⍝ (Tacit) put '[.]' "at" the positions with '.' and flatten
    Sol3 ← ∊(⊂'[.]')@('.'∘=)                      
    
    ⍝ (Dfn) trying to only work with flat arrays
    Sol4 ← {
        exp ← ⍵/⍨1+¯4×d←'.'=⍵
        exp[∊(⍳3)∘.+(⍸d)+2×⍳3] ← 3/'[.]'
        exp
    }
    
    ⍝ Similar to Sol, the variant switches off PCRE
    ⍝ and activates a faster array-based system. By @abrudz.
    Sol5 ← '.'⎕R'[.]'⍠'Regex'0
    
    ⍝ From Vadim Tukaev on YouTube
    Sol6 ← {
        SPLIT ← ≠⊆⊢
        JOIN ← 1↓∘,,⍤0
        ∊(⊂'[.]')∘JOIN '.'∘SPLIT ⍵
    }
    
    ⍝ And a rework of Vadim's solution
    Sol7 ← {3↓∊(⊂'[.]'),¨'.'(≠⊆⊢)⍵}
    
    ∇ r ← RTC
        ⍝ Runtime comparison of the solutions.
        addr ← '127.0.158.1'
        :If 0=⎕NC'cmpx'
            'cmpx'⎕CY'dfns'
        :EndIf
        r ← cmpx 'Sol addr' 'Sol2 addr' 'Sol3 addr' 'Sol4 addr' 'Sol5 addr' 'Sol6 addr' 'Sol7 addr'
    ∇
:EndNamespace


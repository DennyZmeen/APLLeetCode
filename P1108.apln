:Namespace P1108
    ⍝ LeetCode problem #1108
    ⍝ Defanging an IP Address, https://leetcode.com/problems/defanging-an-ip-address
            
    ⍝ (Tacit) regex solution
    Solution ← '\.'⎕R'[.]'
    
    ⍝ (Tacit) split on '.', rejoin with '[.]'
    Solution2 ← ⊃((⊣,'[.]',⊢)/'.'∘≠⊆⊢)
    
    ⍝ (Tacit) put '[.]' "at" the positions with '.' and flatten
    Solution3 ← ∊(⊂'[.]')@('.'∘=)                      
    
    ⍝ (Dfn) trying to only work with flat arrays
    Solution4 ← {
        exp ← ⍵/⍨1+¯4×d←'.'=⍵
        exp[∊(⍳3)∘.+(⍸d)+2×⍳3] ← 3/'[.]'
        exp
    }
    
    ∇ r ← RTC
        ⍝ Runtime comparison of the solutions.
        addr ← '127.0.158.1'
        :If 0=⎕NC'cmpx'
            'cmpx'⎕CY'dfns'
        :EndIf
        r ← cmpx 'Solution addr' 'Solution2 addr' 'Solution3 addr' 'Solution4 addr'
    ∇
:EndNamespace

